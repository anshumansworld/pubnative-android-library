//==================================================================================================
// PROJECT
//==================================================================================================
buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        // FABRIC
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
//==================================================================================================
// ANDROID
//==================================================================================================
apply plugin: 'com.android.application'
android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        applicationId "net.pubnative.library.demo"
        versionCode System.getenv("CIRCLE_BUILD_NUM") ? System.getenv("CIRCLE_BUILD_NUM").toInteger() : 1
        versionName project.version ?: "BETA"
    }
    buildTypes {
        debug {
            ext.betaDistributionGroupAliases = "developers"
        }
        release {
            ext.betaDistributionGroupAliases = "developers"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':library')
    compile project(':library.interstitial')
    compile project(':library.banner')
    compile project(':library.feed.banner')
    compile project(':library.video')
    compile project(':library.feed.video')
    compile project(':library.layouts')
    compile 'com.squareup.picasso:picasso:2.5.2'
}

//==================================================================================================
// CIRCLE CI
//==================================================================================================
if (System.getenv("CIRCLECI")) {

    //----------------------------------------------------------------------------------------------
    // FABRIC
    //----------------------------------------------------------------------------------------------
    apply plugin: 'io.fabric'
    println ""
    println "======== FABRIC ========"
    try {
        File file = new File("${project.projectDir.absolutePath}/fabric.properties")
        if (!file.canRead()) {
            file.createNewFile()
            file << "apiKey=${System.getenv("FABRIC_API_KEY")}\n"
            file << "apiSecret=${System.getenv("FABRIC_API_SECRET")}\n"
        }
        println " - Success"
        println "========================"
        println ""
    } catch (IOException ioException) {
        println " - Error: "
        println " ${ioException.toString()}"
        println "========================"
        println ""
    }
}
