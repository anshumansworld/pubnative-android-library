apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = '1.4.9'
group = 'net.pubnative'

def sharedVersion = version

android {
    compileSdkVersion 10
    buildToolsVersion "22.0.1"
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 10
        versionCode 16
        versionName sharedVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.google.android.gms:play-services-ads:7.5.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'org.droidparts:droidparts:2.6.7'
    compile 'com.google.code.gson:gson:2.3.1'
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                name "pubnative-android-library"
                description "Pubnative API library fro Android"
                url "http://www.pubnative.net"

                // Set your license
                licenses {
                    license {
                        name "MIT"
                        url "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id "cerberillo"
                        name "David Martin"
                        email "david@martinmacias.com"
                    }
                }
                scm {
                    connection "https://github.com/pubnative/pubnative-android-library.git"
                    developerConnection "https://github.com/pubnative/pubnative-android-library.git"
                    url "https://github.com/pubnative/pubnative-android-library"
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.api_key')
    configurations = ['archives']
    publish=true
    pkg {
        userOrg = 'pubnative'
        repo = 'maven'
        name = 'pubnative-android-library'
        websiteUrl = 'https://github.com/pubnative/pubnative-android-library'
        issueTrackerUrl = 'https://github.com/pubnative/pubnative-android-library/issues'
        vcsUrl = 'https://github.com/pubnative/pubnative-android-library.git'
        labels = ['pubnative', 'advertising', 'ad']
        licenses = ['MIT']
        version {
            name=sharedVersion
            vcsTag=sharedVersion
        }
    }
}